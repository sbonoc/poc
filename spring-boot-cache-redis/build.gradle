plugins {
    id 'java'
    id 'scala'
    id 'org.springframework.boot' version '2.6.7'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id "io.freefair.lombok" version "8.14.2"
    id 'io.gatling.gradle' version '3.14.3.7'
    id 'com.avast.gradle.docker-compose' version '0.17.12'
}

group = 'bono.poc'
version = '1.0.0'
sourceCompatibility = '18'
targetCompatibility = '18'

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-aop'
    implementation 'org.springframework.boot:spring-boot-starter-cache'
    implementation 'org.springframework.boot:spring-boot-starter-data-redis'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    // Micrometer for Prometheus
    implementation 'io.micrometer:micrometer-registry-prometheus:1.8.5'
    // Redis client
    implementation 'org.redisson:redisson-spring-boot-starter:3.17.1'
    // Mapstruct
    implementation 'org.mapstruct:mapstruct:1.4.2.Final'
    annotationProcessor 'org.mapstruct:mapstruct-processor:1.4.2.Final'
    // Caffeine cache implementation
    implementation 'com.github.ben-manes.caffeine:caffeine'
    // EhCache cache implementation
    implementation 'org.ehcache:ehcache'
    implementation 'com.sun.xml.bind:jaxb-ri:2.3.6'
    // Geode cache implementation
    implementation 'org.springframework.geode:spring-geode-starter:1.6.7'
    implementation 'org.springframework.geode:spring-geode-starter-actuator:1.6.7'

    // Explicitly add Netty dependencies to resolve potential ClassNotFoundException during Gatling run.
    // This ensures the correct Netty modules and versions are available,
    // especially when multiple libraries (Redisson, Gatling) depend on Netty.
    // Using the version aligned with Spring Boot 2.6.7's BOM.
    implementation 'io.netty:netty-buffer:4.1.76.Final'
    implementation 'io.netty:netty-common:4.1.76.Final'
    implementation 'io.netty:netty-transport:4.1.76.Final'
    implementation 'io.netty:netty-handler:4.1.76.Final'
    implementation 'io.netty:netty-codec:4.1.76.Final'
    implementation 'io.netty:netty-resolver:4.1.76.Final'
}

test {
    useJUnitPlatform()
}

/* DOCKER COMPOSE CONFIG */

dockerCompose.isRequiredBy(test)
dockerCompose.isRequiredBy(bootRun)

dockerCompose {
    useComposeFiles = ['docker/docker-compose.yml']
    forceRecreate = true
}

/* GATLING CONFIG */

// See https://gatling.io/docs/gatling/reference/current/extensions/gradle_plugin/#plugin-configuration
gatling {
    includeMainOutput = false
    includeTestOutput = true
}

// See https://gatling.io/docs/gatling/reference/current/extensions/gradle_plugin/#default-tasks
tasks.withType(io.gatling.gradle.GatlingRunTask) {
    outputs.upToDateWhen { false }
}
